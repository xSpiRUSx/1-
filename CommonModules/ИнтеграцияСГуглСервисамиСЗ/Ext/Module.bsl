
Процедура POSTЗапрос_ПолучениеТокена() Экспорт
	
	СписокНастроек = РегистрыСведений.НастройкиПодключенияГуглСервисов.ПолучитьСписокНастроек();
	

	ДанныеДляЗапроса = Новый Структура;
	ДанныеДляЗапроса.Вставить("grant_type", "authorization_code");
	ДанныеДляЗапроса.Вставить("client_id", СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.ClientID]);
	ДанныеДляЗапроса.Вставить("client_secret", СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.ClientSecret]);
	ДанныеДляЗапроса.Вставить("redirect_uri", "http://localhost");
	ДанныеДляЗапроса.Вставить("code", СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.code]);
	
	Заголовки  = Новый Соответствие;	
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	СтрокаЗапроса = "";
	
	Для Каждого ПараметрЗапроса Из ДанныеДляЗапроса Цикл
		
		  СтрокаЗапроса = СтрокаЗапроса + ПараметрЗапроса.Ключ + "=" + ПараметрЗапроса.Значение + "&";
		  
	КонецЦикла;
	
	СтрокаЗапроса = Лев(СтрокаЗапроса, СтрДлина(СтрокаЗапроса) - 1);
	
	Попытка
		Соединение = Новый HTTPСоединение("accounts.google.com",,,,,,Новый ЗащищенноеСоединениеOpenSSL);
		Запрос = Новый HTTPЗапрос("/o/oauth2/token", Заголовки);
		
		Запрос.УстановитьТелоИзСтроки(СтрокаЗапроса);
		
		Ответ = Соединение.ОтправитьДляОбработки(Запрос);
		
		Если Ответ.КодСостояния = 200 Тогда
			ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
			
			Попытка
				ЧтениеJSON = Новый ЧтениеJSON; 
				ЧтениеJSON.УстановитьСтроку(ТекстОтвета); 
				ЗаписьJSON = ПрочитатьJSON(ЧтениеJSON); 
				ЧтениеJSON.Закрыть();
				
				СтруктураНастроек = Новый Структура("access_token, expires_in, refresh_token");
				
				ЗаполнитьЗначенияСвойств(СтруктураНастроек, ЗаписьJSON);
				
				СтруктураНастроек.Вставить("ДатаПолученияТокена", ТекущаяДата());
				
				РегистрыСведений.НастройкиПодключенияГуглСервисов.ЗаписатьЗначениеНастроек(СтруктураНастроек);				
				
			Исключение
				ОбщегоНазначения.СообщитьПользователю("Ошибка при разборе JSON");
			КонецПопытки;
	
		Иначе
			ЗаписьЖурналаРегистрации("Авторизация на сервере", УровеньЖурналаРегистрации.Ошибка,,,"Ошибка. Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку());
		КонецЕсли;
			
	Исключение
		ЗаписьЖурналаРегистрации("Авторизация на сервере", УровеньЖурналаРегистрации.Ошибка,,,"Ошибка при выполнении HTTP запроса:" + ОписаниеОшибки());
	КонецПопытки

КонецПроцедуры

Функция POSTЗапрос_ОбновитьТокен() Экспорт
	
	СписокНастроек = РегистрыСведений.НастройкиПодключенияГуглСервисов.ПолучитьСписокНастроек();
	
	ДанныеДляЗапроса = Новый Структура;
	ДанныеДляЗапроса.Вставить("grant_type", "refresh_token");
	ДанныеДляЗапроса.Вставить("client_id", СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.ClientID]);
	ДанныеДляЗапроса.Вставить("client_secret", СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.ClientSecret]);
	ДанныеДляЗапроса.Вставить("refresh_token", СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.refresh_token]);
	
	Заголовки  = Новый Соответствие;	
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	
	СтрокаЗапроса = "";
	
	Для Каждого ПараметрЗапроса Из ДанныеДляЗапроса Цикл
		
		  СтрокаЗапроса = СтрокаЗапроса + ПараметрЗапроса.Ключ + "=" + ПараметрЗапроса.Значение + "&";
		  
	КонецЦикла;
	
	СтрокаЗапроса = Лев(СтрокаЗапроса, СтрДлина(СтрокаЗапроса) - 1);
	
	Попытка
		Соединение = Новый HTTPСоединение("oauth2.googleapis.com",,,,,,Новый ЗащищенноеСоединениеOpenSSL);
		Запрос = Новый HTTPЗапрос("/token", Заголовки);
		
		Запрос.УстановитьТелоИзСтроки(СтрокаЗапроса);
		
		Ответ = Соединение.ОтправитьДляОбработки(Запрос);
		
		Если Ответ.КодСостояния = 200 Тогда
			ТекстОтвета = Ответ.ПолучитьТелоКакСтроку();
			
			Попытка
				ЧтениеJSON = Новый ЧтениеJSON; 
				ЧтениеJSON.УстановитьСтроку(ТекстОтвета); 
				ЗаписьJSON = ПрочитатьJSON(ЧтениеJSON); 
				ЧтениеJSON.Закрыть();
				
				СтруктураНастроек = Новый Структура("access_token, expires_in");
				
				ЗаполнитьЗначенияСвойств(СтруктураНастроек, ЗаписьJSON);
				
				СтруктураНастроек.Вставить("ДатаПолученияТокена", ТекущаяДата());
				
				РегистрыСведений.НастройкиПодключенияГуглСервисов.ЗаписатьЗначениеНастроек(СтруктураНастроек);				
				
			Исключение
				ЗаписьЖурналаРегистрации("Авторизация на сервере", УровеньЖурналаРегистрации.Ошибка,,,"Ошибка при разборе JSON: " + Ответ.ПолучитьТелоКакСтроку());
				Возврат Неопределено
			КонецПопытки;
	
		Иначе
			ЗаписьЖурналаРегистрации("Авторизация на сервере", УровеньЖурналаРегистрации.Ошибка,,,"Ошибка. Код ответа " + Ответ.КодСостояния + ": " + Ответ.ПолучитьТелоКакСтроку());
			Возврат Неопределено
		КонецЕсли;
			
	Исключение
		ЗаписьЖурналаРегистрации("Авторизация на сервере", УровеньЖурналаРегистрации.Ошибка,,,"Ошибка при выполнении HTTP запроса:" + ОписаниеОшибки());
		Возврат Неопределено
	КонецПопытки;

	Возврат ЗаписьJSON.access_token;
	
КонецФункции

