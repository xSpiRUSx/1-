

Процедура ЗаписатьЗначениеНастройки(КлючНастройки, ЗначениеНастройки) Экспорт
	
	Запись = РегистрыСведений.НастройкиПодключенияГуглСервисов.СоздатьМенеджерЗаписи();
	Запись.ВидНастройки = КлючНастройки;
	Запись.Значение = ЗначениеНастройки;
	Запись.Записать();
	
КонецПроцедуры

Процедура ЗаписатьЗначениеНастроек(СтруктураНастроек) Экспорт
	
	Для Каждого Настройка Из СтруктураНастроек Цикл
		
		Запись = РегистрыСведений.НастройкиПодключенияГуглСервисов.СоздатьМенеджерЗаписи();
		Запись.ВидНастройки = Перечисления.ВариантыНастроекГуглСервисов[Настройка.Ключ];
		Запись.Значение = Настройка.Значение;
		Запись.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСписокНастроек() Экспорт
	
	СписокНастроек = Новый Соответствие;
	
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВариантыНастроекГуглСервисов.ЗначенияПеречисления Цикл
		
		 СписокНастроек.Вставить(Перечисления.ВариантыНастроекГуглСервисов[ЗначениеПеречисления.Имя], "");
		
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.НастройкиПодключенияГуглСервисов.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	Для Каждого ТекущаяЗапись Из НаборЗаписей Цикл
		СписокНастроек.Вставить(ТекущаяЗапись.ВидНастройки, ТекущаяЗапись.Значение);
	КонецЦикла;
	
	Возврат СписокНастроек;
	
КонецФункции

Функция ПолучитьТокен() Экспорт
	
	СписокНастроек = ПолучитьСписокНастроек();
	
	Если СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.ДатаПолученияТокена] + 
		СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.expires_in] < ТекущаяДата() + 60 Тогда
		
		Токен = ИнтеграцияСГуглСервисамиСЗ.POSTЗапрос_ОбновитьТокен();
		
	Иначе
		
		Токен = СписокНастроек[Перечисления.ВариантыНастроекГуглСервисов.access_token];
		
	КонецЕсли;
	
	Возврат Токен;
		
КонецФункции

